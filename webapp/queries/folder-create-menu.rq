#
# @Cache-Control: 
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX calli: <http://callimachusproject.org/rdf/2009/framework#>

SELECT (concat('?create=', encode_for_uri(str(?cls))) AS ?url)
    ?label ?comment ?icon (min(?m) AS ?menu) (min(?s) AS ?section)
WHERE {
    ?cls rdf:type/rdfs:subClassOf? <../types/Creatable>;
        rdfs:label ?label;
        rdfs:comment ?comment;
        calli:icon ?icon;
        calli:create ?create
    FILTER (?create != <../templates/file-create.xhtml>)
    {
        ?cls calli:author </auth/groups/everyone>
        BIND (1 AS ?m)
        BIND (1 AS ?s)
    } UNION {
        ?cls calli:author </auth/groups/users>
        BIND (1 AS ?m)
        BIND (2 AS ?s)
    } UNION {
        ?cls calli:author </auth/groups/staff>
        BIND (2 AS ?m)
        BIND (3 AS ?s)
    } UNION {
        ?cls calli:author </auth/groups/power>
        BIND (3 AS ?m)
        BIND (4 AS ?s)
    } UNION {
        ?cls calli:author </auth/groups/admin>
        BIND (3 AS ?m)
        BIND (5 AS ?s)
    } UNION {
        BIND (3 AS ?m)
        BIND (6 AS ?s)
    }
} GROUP BY ?cls ?label ?comment ?icon ORDER BY ?menu ?section ?label
