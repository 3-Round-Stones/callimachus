PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl:<http://www.w3.org/2008/05/skos-xl#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX dcterms:<http://purl.org/dc/terms/>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
PREFIX prov:<http://www.w3.org/ns/prov#>
PREFIX calli:<http://callimachusproject.org/rdf/2009/framework#>
SELECT * {
    {
        <$this> rdfs:label ?title
        BIND (concat(str(<$this>),"?contributions") as ?id)
        BIND ("feed" as ?type)
    } UNION {
        <$this> prov:wasGeneratedBy [prov:endedAtTime ?updated]
        BIND (concat(str(<$this>),"?contributions") as ?id)
        BIND ("feed" as ?type)
    } UNION {
        <$this> rdfs:label ?contributor_name
        BIND (<$this> as ?contributor_uri)
        BIND ("feed" as ?type)
    } UNION {
        {
            SELECT ?id (max(?title)  as ?title) (max(?updated) as ?updated) (max(?summary) as ?summary) (max(?icon) as ?icon)
                    (?id as ?content_src) (max(?content_type) as ?content_type) ("entry" as ?type)
                    (?id as ?link_edit_media_href) (concat(str(?id),"?view") as ?link_view_href) (concat(str(?id),"?history") as ?link_history_href)
                    (concat(str(?revision),"?view") as ?link_href) (prov:wasGeneratedBy as ?link_rel) ("text/html" as ?link_type) {
                {
                    SELECT * {
                        ?revision prov:wasAssociatedWith <$this>; prov:endedAtTime ?updated .
                        ?id prov:wasGeneratedBy ?revision .
                    } ORDER BY desc(?updated) LIMIT 50
                } OPTIONAL {
                    ?id skos:prefLabel ?title
                } OPTIONAL {
                    ?id foaf:name ?title
                } OPTIONAL {
                    ?id rdfs:label ?title
                } OPTIONAL {
                    ?id skosxl:literalForm ?title
                } OPTIONAL {
                    ?id dcterms:title ?title
                } OPTIONAL {
                    ?id rdfs:comment ?summary
                } OPTIONAL {
                    ?id a [calli:icon ?icon]
                } OPTIONAL {
                    ?id a [calli:type ?content_type]
                }
            } GROUP BY ?id ?revision
        }
        FILTER bound(?id)
    }
} ORDER BY desc(?type) desc(?updated) ?id