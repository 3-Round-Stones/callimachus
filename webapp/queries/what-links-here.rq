#
# Copyright (c) 2014 3 Round Stones Inc., Some Rights Reserved
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#
#
# @Cache-Control: no-store
#
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX prov:<http://www.w3.org/ns/prov#>
PREFIX calli:<http://callimachusproject.org/rdf/2009/framework#>
SELECT DISTINCT ?label ?comment ?icon ?url ?modified
WHERE {
    ?url ?rel <$target>
    FILTER (isIRI(?url))
    FILTER (?rel != prov:wasAssociatedWith)
    {
        FILTER (!contains(str(?url), "?") && !contains(str(?url), "#"))
        ?url prov:wasGeneratedBy ?revision
        OPTIONAL { ?revision prov:endedAtTime ?modified }
        OPTIONAL { ?url a [calli:icon ?icon] }
        OPTIONAL { ?url skos:prefLabel ?label }
        OPTIONAL { ?url rdfs:label ?label }
        OPTIONAL { ?url rdfs:comment ?comment }
    } UNION {
        FILTER contains(str(?url), "?")
        BIND (iri(strbefore(str(?url), "?")) AS ?entity)
        ?entity prov:wasGeneratedBy ?revision
        OPTIONAL { ?revision prov:endedAtTime ?modified }
        OPTIONAL { ?entity a [calli:icon ?icon] }
        OPTIONAL { ?entity skos:prefLabel ?label }
        OPTIONAL { ?entity rdfs:label ?label }
        OPTIONAL { ?entity rdfs:comment ?comment }
    } UNION {
        FILTER (!contains(str(?url), "?"))
        FILTER contains(str(?url), "#")
        BIND (iri(strbefore(str(?url), "#")) AS ?entity)
        ?entity prov:wasGeneratedBy ?revision
        OPTIONAL { ?revision prov:endedAtTime ?modified }
        OPTIONAL { ?entity a [calli:icon ?icon] }
        OPTIONAL { ?entity skos:prefLabel ?label }
        OPTIONAL { ?entity rdfs:label ?label }
        OPTIONAL { ?entity rdfs:comment ?comment }
    }
}
ORDER BY ?label ?url ?icon
LIMIT 100
