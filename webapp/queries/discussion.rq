#
# Copyright (c) 2014 3 Round Stones Inc., Some Rights Reserved
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#
#
# @Cache-Control: no-store
#
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX prov:<http://www.w3.org/ns/prov#>
SELECT REDUCED ?label ?note ?revision ?modified ?user (coalesce(?uname, replace(str(?user),"^.*/","")) AS ?name)
WHERE {
    {
        <$target> skos:prefLabel ?label
    } UNION {
        <$target> rdfs:label ?label
    } UNION {
        GRAPH ?revision { <$target> skos:editorialNote ?note }
        ?revision prov:wasGeneratedBy ?activity .
        ?activity prov:endedAtTime ?modified .
        OPTIONAL { ?activity prov:wasAssociatedWith ?user
            OPTIONAL { ?user rdfs:label ?uname }
        }
    }
}
ORDER BY ?modified
LIMIT 100
