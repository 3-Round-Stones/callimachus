#
# Copyright (c) 2014 3 Round Stones Inc., Some Rights Reserved
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#
#
# @Cache-Control: no-store
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX calli: <http://callimachusproject.org/rdf/2009/framework#>

SELECT (concat('?create=', encode_for_uri(str(?cls))) AS ?url)
    ?label ?comment ?icon (substr(min(?order),2) AS ?author)
WHERE {
    ?cls calli:create ?create;
        rdfs:label ?label
    FILTER (?create != <../templates/file-create.xhtml>)
    FILTER EXISTS {
        ?cls calli:author ?domain
        {
            FILTER (?domain = <$user>)
        } UNION {
            ?domain calli:member <$user>
        } UNION {
            ?domain calli:everyoneFrom []
        } UNION {
            ?domain calli:anonymousFrom []
        }
    }
    ?cls calli:author ?group .
    ?group rdfs:label ?author .
    OPTIONAL {
        ?cls calli:icon ?icon
    }
    OPTIONAL {
        ?cls rdfs:comment ?comment
    }
    BIND (
        if (bound(?comment),
            concat(
                if (?group = </auth/groups/everyone>, "1",
                if (?group = </auth/groups/users>, "2",
                if (?group = </auth/groups/staff>, "3",
                if (?group = </auth/groups/power>, "4",
                if (?group = </auth/groups/admin>, "5",
                "6"
                ))))),
                ?author
            ), "9"
        ) AS ?order
    )
} GROUP BY ?cls ?label ?comment ?icon ?with_icon ORDER BY asc(min(?order)) ?author ?label
