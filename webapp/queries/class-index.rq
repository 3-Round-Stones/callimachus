#
# Copyright (c) 2014 3 Round Stones Inc., Some Rights Reserved
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#
#
# @Cache-Control: no-store
#
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
SELECT REDUCED ?title ?description ?resource ?label (ucase(substr(?label, 1, 1)) as ?index) (regex(?label, "^[a-zA-Z]") as ?letter) {
    {
        <$target> rdfs:label ?title
        OPTIONAL {
            <$target> rdfs:comment ?description
        }
    } UNION {
        ?resource a <$target>
        OPTIONAL {
            ?resource skos:prefLabel ?label
        } OPTIONAL {
            ?resource rdfs:label ?label
        } OPTIONAL {
            ?resource foaf:name ?label
        }
    } UNION {
        <$target> owl:equivalentClass ?type . ?resource a ?type
        OPTIONAL {
            ?resource skos:prefLabel ?label
        } OPTIONAL {
            ?resource rdfs:label ?label
        } OPTIONAL {
            ?resource foaf:name ?label
        }
    }
} ORDER BY ?label ?resource LIMIT 1001
