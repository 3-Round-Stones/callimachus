#
# @Cache-Control: no-store
#
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX prov:<http://www.w3.org/ns/prov#>
PREFIX audit:<http://www.openrdf.org/rdf/2012/auditing#>
PREFIX calli:<http://callimachusproject.org/rdf/2009/framework#>
SELECT REDUCED ?modified ?user ?name ?previous ?subsequent ?removed ?added ?subject ?predicate ?object
WHERE {
    {
        <$target> prov:wasGeneratedBy ?provenance .
        ?provenance prov:endedAtTime ?modified
        OPTIONAL {
            ?provenance prov:wasAssociatedWith ?user
            OPTIONAL { ?user skos:prefLabel ?name }
            OPTIONAL { ?user rdfs:label ?name }
        }
    } UNION {
        <$target> prov:wasInfluencedBy ?previous . ?previous prov:wasGeneratedBy [prov:endedAtTime ?asof]
    } UNION {
        ?subsequent prov:wasInfluencedBy <$target> . ?subsequent prov:wasGeneratedBy [prov:endedAtTime ?asof]
    } UNION {
        GRAPH ?added {
            ?subject ?predicate ?object
            FILTER (!strstarts(str(?subject),str(?added)))
            FILTER (!strstarts(str(?predicate),str(prov:)))
            FILTER (!strstarts(str(?predicate),str(audit:)))
            FILTER (?predicate != rdf:subject)
            FILTER (?predicate != rdf:predicate)
            FILTER (?predicate != rdf:object)
        }
        ?added prov:wasGeneratedBy [prov:endedAtTime ?asof]
        FILTER (?added = <$target>)
    } UNION {
        GRAPH ?added {
            ?entity audit:with ?triple
        }
        GRAPH ?removed {
            ?removed prov:wasInfluencedBy ?added .
            [audit:without ?triple] .
            ?triple rdf:subject ?subject;
                    rdf:predicate ?predicate;
                    rdf:object ?object .
        }
        OPTIONAL { ?activity prov:generated ?entity ; prov:endedAtTime ?asof }
        FILTER (?added = <$target> || ?removed = <$target>)
    }
}
ORDER BY ?subject ?predicate ?asof ?object
