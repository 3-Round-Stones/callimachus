#
# Copyright (c) 2014 3 Round Stones Inc., Some Rights Reserved
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#
#
# @Cache-Control: no-store
#
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl:<http://www.w3.org/2008/05/skos-xl#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX dcterms:<http://purl.org/dc/terms/>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
PREFIX keyword:<http://www.openrdf.org/rdf/2011/keyword#>
PREFIX prov:<http://www.w3.org/ns/prov#>
PREFIX calli:<http://callimachusproject.org/rdf/2009/framework#>
SELECT * {
    {
        <$target> rdfs:label ?title
        BIND (concat(str(<$target>),"?q=",encode_for_uri("$q")) as ?id)
        BIND ("feed" as ?type)
    } UNION {
        <$target> prov:wasGeneratedBy [prov:endedAtTime ?updated]
        BIND (concat(str(<$target>),"?q=",encode_for_uri("$q")) as ?id)
        BIND ("feed" as ?type)
    } UNION {
        {
            SELECT DISTINCT (max(?label) as ?title) (max(?comment) as ?summary) (max(?icon) as ?icon) (?url as ?id) (max(?modified) as ?updated) (max(?content_type) as ?content_type)
            WHERE {
                {
                    SELECT * {
                        {
                            SELECT * {
                                ?url keyword:phone ?soundex
                                FILTER sameTerm(?soundex, keyword:soundex("$q"))
                                FILTER (strlen("$q") > 0)
                            } LIMIT 1000
                        }
                        FILTER (isIRI(?url))
                        FILTER (
                            strstarts(str(?url), str(<$target>)) ||
                            <$target> = </> && (
                                EXISTS {
                                    {
                                        ?realm a <../types/Origin>
                                        FILTER strstarts(str(?url), str(?realm))
                                    } UNION {
                                        ?realm a <../types/Realm>
                                        FILTER strstarts(str(?url), str(?realm))
                                    } UNION {
                                        <../types/Serviceable> owl:equivalentClass rdfs:Resource
                                    }
                                }
                            )
                        )
                        FILTER EXISTS {
                            ?url ?p ?string
                            FILTER isLiteral(?string)
                            FILTER regex(?string, keyword:regex("$q"))
                        }
                    } ORDER BY ?url LIMIT 100
                } OPTIONAL {
                    ?url skos:prefLabel ?label
                } OPTIONAL {
                    ?url foaf:name ?label
                } OPTIONAL {
                    ?url rdfs:label ?label
                } OPTIONAL {
                    ?url skosxl:literalForm ?label
                } OPTIONAL {
                    ?url dcterms:title ?label
                }
                OPTIONAL { ?url a [calli:icon ?icon] }
                OPTIONAL { ?url rdfs:comment ?comment }
                OPTIONAL { ?url prov:wasGeneratedBy ?revision
                    OPTIONAL { ?revision prov:endedAtTime ?modified }
                } OPTIONAL {
                    ?url a [calli:type ?content_type]
                }
            }
            GROUP BY ?url ORDER BY ?title ?id ?icon
        }
        FILTER (bound(?id))
        FILTER (strlen("$q") > 0)
        BIND ("entry" as ?type)
        BIND (concat(str(?id),"?view") as ?link_href)
        BIND ("alternate" as ?link_rel)
        BIND ("text/html" as ?link_type)
        BIND (?id as ?content_src)
    }
}
