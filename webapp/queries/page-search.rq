#
# @Cache-Control: no-store
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
PREFIX calli:<http://callimachusproject.org/rdf/2009/framework#>
PREFIX keyword:<http://www.openrdf.org/rdf/2011/keyword#>

SELECT REDUCED ?resource ?label ?comment {
    {
        SELECT REDUCED ?resource {
            {
                ?resource a foaf:Document, ?page; keyword:phone ?soundex .
                <../types/Page> owl:equivalentClass? ?page
                FILTER sameTerm(?soundex, keyword:soundex("$q"))
                FILTER (!strstarts(str(?resource),str(<../../>)))
            } UNION {
                ?resource a foaf:Document, ?page .
                <../types/Page> owl:equivalentClass? ?page
                FILTER sameTerm(?resource, iri(<$iri>))
            }
        } LIMIT 100
    }
    FILTER (isIRI(?resource))
    ?resource rdfs:label ?label
    FILTER isLiteral(?label)
    FILTER (strlen("$q") < 3 || regex(?label, keyword:regex("$q")))
    OPTIONAL {
        ?resource rdfs:comment ?comment
    }
} ORDER BY ?label LIMIT 100
