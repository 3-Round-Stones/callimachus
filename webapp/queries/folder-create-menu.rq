#
# @Cache-Control: 
# @view 
#
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX calli: <http://callimachusproject.org/rdf/2009/framework#>

SELECT (concat('?create=', encode_for_uri(str(?cls))) AS ?url)
    ?label ?comment ?icon (substr(min(?order),2) AS ?author)
WHERE {
    ?cls rdf:type/rdfs:subClassOf? <../types/Creatable>;
        rdfs:label ?label;
        rdfs:comment ?comment;
        calli:icon ?icon;
        calli:create ?create
    FILTER (?create != <../templates/file-create.xhtml>)
    {
        ?cls calli:author </auth/groups/everyone> .
        </auth/groups/everyone> rdfs:label ?author
        BIND (concat("1", ?author) AS ?order)
    } UNION {
        ?cls calli:author </auth/groups/users> .
        </auth/groups/users> rdfs:label ?author
        BIND (concat("2", ?author) AS ?order)
    } UNION {
        ?cls calli:author </auth/groups/staff> .
        </auth/groups/staff> rdfs:label ?author
        BIND (concat("3", ?author) AS ?order)
    } UNION {
        ?cls calli:author </auth/groups/power> .
        </auth/groups/power> rdfs:label ?author
        BIND (concat("4", ?author) AS ?order)
    } UNION {
        ?cls calli:author </auth/groups/admin> .
        </auth/groups/admin> rdfs:label ?author
        BIND (concat("5", ?author) AS ?order)
    } UNION {
        ?cls calli:author [rdfs:label ?author]
        BIND (concat("6", ?author) AS ?order)
    }
} GROUP BY ?cls ?label ?comment ?icon ORDER BY asc(min(?order)) ?author ?label
