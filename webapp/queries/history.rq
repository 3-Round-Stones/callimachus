#
# @Cache-Control: no-store
#
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl:<http://www.w3.org/2008/05/skos-xl#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX dcterms:<http://purl.org/dc/terms/>
PREFIX foaf:<http://xmlns.com/foaf/0.1/>
PREFIX prov:<http://www.w3.org/ns/prov#>
PREFIX calli:<http://callimachusproject.org/rdf/2009/framework#>
SELECT * {
    {
        <$target> rdfs:label ?title
        BIND (concat(str(<$target>),"?history") as ?id)
        BIND ("feed" as ?type)
    } UNION {
        <$target> prov:wasGeneratedBy [prov:endedAtTime ?updated]
        BIND (concat(str(<$target>),"?history") as ?id)
        BIND ("feed" as ?type)
    } UNION {
        {
            SELECT ?id (max(?title) as ?title) (max(?updated) as ?updated) (max(?summary) as ?summary) (max(?icon) as ?icon)
                    ("entry" as ?type) (concat(str(?id),"?view") as ?link_view_href)
                    (max(?contributor_uri) as ?contributor_uri) (max(?contributor_name) as ?contributor_name)  {
                {
                    SELECT ?id ?updated {
                        ?id prov:generated [prov:specializationOf <$target>]; prov:endedAtTime ?updated
                    }  ORDER BY desc(?updated) LIMIT 50
                } OPTIONAL {
                    ?id prov:wasAssociatedWith ?contributor_uri
                    OPTIONAL {
                        ?contributor_uri rdfs:label ?contributor_name
                    }
                } OPTIONAL {
                    <$target> skos:prefLabel ?title
                } OPTIONAL {
                    <$target> foaf:name ?title
                } OPTIONAL {
                    <$target> rdfs:label ?title
                } OPTIONAL {
                    <$target> skosxl:literalForm ?title
                } OPTIONAL {
                    <$target> dcterms:title ?title
                } OPTIONAL {
                    GRAPH ?graph { ?graph prov:wasGeneratedBy ?id . <$target> skos:changeNote ?summary }
                } OPTIONAL {
                    <$target> a [calli:icon ?icon]
                }
            } GROUP BY ?id
        }
        FILTER bound(?id)
    }
} ORDER BY desc(?type) desc(?updated) ?id
